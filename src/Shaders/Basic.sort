@Technique default{
@Pass{
@BlendFunc(SRC_ALPHA,ONE_MINUS_SRC_ALPHA)
FS_PREC(mediump,float)

varying vec3 vNormal;
varying vec2 vTexCoord;
varying vec4 vPosition;

#ifdef VS
  attribute vec3 position;
  attribute vec3 normal;
  attribute vec2 texCoord;

  uniform mat4 _matPVM;
  uniform mat4 _matM;

  void main(){
    gl_Position = _matPVM * vec4(position,1.0);
    vNormal = normalize((_matM * vec4(normal,0.0)).xyz);
    vPosition = (_matM * vec4(position,1.0));
    vTexCoord = texCoord;
  }
#endif

#ifdef FS
  @CAMERA_POSITION
  uniform vec3 _cameraPosition;

  uniform sampler2D texture;

  @HAS_TEXTURE{sampler:"texture"}
  uniform bool useTexture;

  uniform sampler2D roughnessMap;

  @HAS_TEXTURE{sampler:"roughnessMap"}
  uniform bool useRoughnessMap;

  @{default:"white",type:"color"}
  uniform vec4 albedo;

  @{default:1.0}
  uniform float roughness;

  @{default:0.99}
  uniform float metalic;

  @{default:"black",type:"color"}
  uniform vec3 emission;

  @import "forward-shading"

  void main(){
    vec4 lastColor = vec4(0,0,0,albedo.a);
    vec3 dBaseColor = albedo.rgb;
    if(useTexture){
      vec4 texColor = texture2D(texture,vTexCoord);
      dBaseColor = pow(texColor.rgb,vec3(2.2));
      lastColor.a = texColor.a;
    }
    float r = roughness;
    if(useRoughnessMap){
      r *= texture2D(roughnessMap,vTexCoord).r;
    }
    vec3 dielectricSpecular = vec3(0.04);
    vec3 diffuse = mix(dBaseColor.rgb * (1. - dielectricSpecular.r),vec3(0),metalic);
    vec3 f0 = mix(dielectricSpecular,dBaseColor.rgb,metalic);
    BRDF_PARAMS param = BRDF_PARAMS(diffuse,f0,roughness);
    lastColor.rgb += emission + shading(param,vNormal,vPosition.xyz/vPosition.w);
    gl_FragColor = lastColor;
    gl_FragColor.rgb = pow(gl_FragColor.rgb,vec3(1.0/2.2)); // Gamma correction
    gl_FragColor.a = lastColor.a;
  }
#endif
}
}

@Technique hitarea{
  @Disable(BLEND)
  @Pass{
    FS_PREC(mediump,float)

    #ifdef VS
      attribute vec3 position;
      uniform mat4 _matPVM;

      void main(){
        gl_Position = _matPVM * vec4(position,1.0);
      }
    #endif

    #ifdef FS
      @MESH_INDEX
      uniform vec4 meshIndex;
      void main(){
        gl_FragColor = meshIndex;
      }
    #endif
  }
}

@Technique depth{
  @Pass{
    @CullFace(FRONT)
  FS_PREC(highp,float)

  varying vec4 vPos;

  vec3 EncodeFloatRGB( float v ) {
    vec3 enc = vec3(1.0, 255.0, 65025.0) * v;
    enc = fract(enc);
    enc -= enc.yzz * vec3(1.0/255.0,1.0/255.0,0.0);
    return enc;
  }

  float DecodeFloatRGB( vec3 rgb ) {
    return dot( rgb, vec3(1.0, 1./255.0, 1./65025.0) );
  }

  #ifdef VS
    attribute vec3 position;
    uniform mat4 _matPVM;
    void main(){
      gl_Position = vPos= _matPVM * vec4(position,1.0);
    }
  #endif

  #ifdef FS
    void main(){
      gl_FragColor.rgb = EncodeFloatRGB((vPos.z/vPos.w + 1.0)/2.0);
      gl_FragColor.a = 1.0;
    }
  #endif
  }
}
